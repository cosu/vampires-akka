apply plugin: 'idea'
apply plugin: 'base'
apply plugin: 'distribution'


idea{
    project {
        languageLevel = '1.8'
    }
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

wrapper {
    gradleVersion = '2.9'
}


allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    task deleteLocalInstall(type: Delete) {
        delete System.properties['user.home'] + "/vampires-akka/${project.name}"
    }

    task copyScripts(dependsOn: deleteLocalInstall, type: Copy){
        from "scripts"
        into System.properties['user.home'] + "/vampires-akka/scripts"

    }

    task installLocal(dependsOn: [installDist, copyScripts],type: Sync) {
        from "${buildDir}/install/${project.name}"
        into System.properties['user.home'] + "/vampires-akka/${project.name}"
    }


}


subprojects {
    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'idea'
    apply plugin: 'jacoco'



    idea {
        module {
            generatedSourceDirs += file('generated')
        }
    }

    compileJava {
        targetCompatibility= 1.8
        sourceCompatibility= 1.8
        options.fork = true

        //enable incremental compilation
        options.incremental = true
    }

    group 'ro.cosu.vampires'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    sourceSets {
        integrationTest {
            java.srcDir file('src/integTest/java')
            resources.srcDir file('src/integTest/resources')
        }
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath

    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport


//    check.dependsOn integrationTest
    integrationTest.dependsOn test

    dependencies {
        def akkaVersion = "2.11:2.4.1"

        compile "com.typesafe.akka:akka-actor_${akkaVersion}"
        compile "com.typesafe.akka:akka-remote_${akkaVersion}"
        compile "com.typesafe.akka:akka-slf4j_${akkaVersion}"

        compile 'com.google.guava:guava:18.0'
        compile 'com.google.inject:guice:4.0'
        compile 'com.google.inject.extensions:guice-multibindings:4.0'
        compile 'com.google.auto.value:auto-value:1.1'
        compile 'com.google.code.gson:gson:2.5'
        compile 'com.google.code.findbugs:jsr305:3.0.1'


        compile 'ch.qos.logback:logback-classic:1.1.3'
        compile 'org.apache.commons:commons-exec:1.3'
        compile 'org.slf4j:jul-to-slf4j:1.7.13'

        testCompile 'junit:junit:4.12'
        testCompile "com.typesafe.akka:akka-testkit_${akkaVersion}"
        testCompile "org.mockito:mockito-core:1.+"

        integrationTestCompile sourceSets.main.output
        integrationTestCompile sourceSets.test.output
        integrationTestCompile configurations.testCompile
        integrationTestRuntime configurations.testRuntime

    }








}

project(':client') {

    mainClassName = "ro.cosu.vampires.client.Client"
    dependencies {
        compile project(':server')
        compile 'io.kamon:sigar-loader:1.6.6-rev002'
        compile 'io.dropwizard.metrics:metrics-core:3.1.2'
        compile 'com.github.docker-java:docker-java:2.1.2'
    }
}

project(':server'){
    mainClassName = "ro.cosu.vampires.server.Server"
    dependencies {
        compile 'org.mongodb.morphia:morphia:1.0.1'
        compile 'com.jcraft:jsch:0.1.53'
        compile 'org.mongodb.morphia:morphia-logging-slf4j:1.0.1'
        compile 'com.amazonaws:aws-java-sdk:1.10.39'

        compile ('com.sparkjava:spark-core:2.3') {
            exclude group: 'org.slf4j', module:'slf4j-simple'
        }
    }
}


